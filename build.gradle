plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
}

def javaVersion = findProperty("java.version")?.toString()?.toInteger()

allprojects {
    group = 'fr.nhsoul.natsbridge'
    version = findProperty("plugin.version")

    repositories {
        mavenCentral()
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)
        withSourcesJar()
        withJavadocJar()
    }

    dependencies {
        // NATS client library
        implementation 'io.nats:jnats:2.17.2'

        // Logging
        implementation 'org.slf4j:slf4j-api:2.0.9'

        // Configuration (YAML)
        implementation 'org.yaml:snakeyaml:2.2'

        // Testing
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
        testImplementation 'org.mockito:mockito-core:5.7.0'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.release = javaVersion
    }

    compileTestJava {
        options.encoding = 'UTF-8'
        options.release = javaVersion
    }
}

dependencies {
    implementation project(":common")
    implementation project(":core")
    implementation project(':velocity')
    implementation project(':spigot')
    implementation project(':bungeecord')
}

shadowJar {
    archiveBaseName.set("NatsBridge")
    archiveClassifier.set("") // pas de "-all"
    archiveVersion.set(version)

    mergeServiceFiles() // si META-INF/services utilisÃ©s
}

tasks.named("build") {
    dependsOn(tasks.named("shadowJar"))
}


subprojects { subproj ->
    // Appliquer seulement si le module a le plugin Java (ou Java Library)
    subproj.plugins.withType(JavaPlugin) {
        subproj.apply plugin: 'maven-publish'

        subproj.publishing {
            repositories {
                maven {
                    name = "NhSoulRepo"
                    url = uri("https://repo.nhsoul.fr/releases")
                    credentials(PasswordCredentials) {
                        username = System.getenv("REPO_USERNAME")
                        password = System.getenv("REPO_SECRET")
                    }
                }
            }
            publications {
                create("gpr", MavenPublication) {
                    from subproj.components.java

                    groupId = "fr.nhsoul.natsbridge"
                    artifactId = subproj.name
                    version = rootProject.findProperty("plugin.version")
                }
            }
        }
    }
}


