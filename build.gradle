plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
}

// Appliquer les configurations de versions centralisées
apply from: 'gradle/versions.gradle'

allprojects {
    group = 'fr.nhsoul.natsbridge'
    version = findProperty("plugin.version")

    repositories {
        mavenCentral()
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

// Appliquer les plugins de base à tous les sous-projets
subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply from: rootProject.file('gradle/convention-plugins/java-library.gradle')
}

// Configuration pour le projet racine (shadowJar)
shadowJar {
    archiveBaseName.set("NatsBridge")
    archiveClassifier.set("") // pas de "-all"
    archiveVersion.set(version)
    mergeServiceFiles() // si META-INF/services utilisés
}

tasks.named("build") {
    dependsOn(tasks.named("shadowJar"))
}

// Configuration des dépendances entre modules
dependencies {
    implementation project(":common")
    implementation project(":core")
    implementation project(':velocity')
    implementation project(':spigot')
    implementation project(':bungeecord')
}

// Configuration de publication pour les sous-projets
subprojects { subproj ->
    // Appliquer la publication Maven aux projets Java
    subproj.plugins.withType(JavaPlugin) {
        subproj.apply plugin: 'maven-publish'
        subproj.apply from: rootProject.file('gradle/convention-plugins/maven-publish.gradle')
    }
}


